builtin emulate -L zsh ${=${options[xtrace]:#off}:+-o xtrace}
builtin setopt extended_glob warn_create_global typeset_silent no_short_loops rc_quotes no_auto_pushd

if [[ "$1" = plugin ]] {
    local type="$1" user="$2" plugin="$3" id_as="$4" dir="${5#%}" hook="$6"
} else {
    local type="$1" url="$2" id_as="$3" dir="${4#%}" hook="$5"
}

if (( ${+ICE[lman]} )) {
    [[ -d ${ZI[MAN_DIR]}/man1 ]] || \
        { +zi-message "{annex}linkman-annex{ehi}: {warn}Warning: {msg}Missing {url}${ZI[MAN_DIR]}/man1{msg}, not deleting man pages{â€¦}{rst}" && return}
    local manfiles=${dir}/**/*(.[1-9]|.[1-9].gz)(N.)
    declare -a manfile installed attempted failed
    for manfile ( $~manfiles ); do
        (( ${attempted[(I)$manfile:t]} )) && continue
        attempted+=( $manfile:t )
        { test -f $manfile } && {
            command rm -f -- "${ZI[MAN_DIR]}/man${${${manfile:t}//[!1-9]}[-1]}/$manfile:t" && \
            installed+=( "{obj}$manfile:t" )
        } || {
            failed+=( "{obj}$manfile:t" )
        }
    done
    if (( !OPTS[opt_-q,--quiet] )) {
        if (( ${#installed} )) {
            +zi-message "{annex}linkman-annex{ehi}: {msg}Removed man page${${${#installed}/1/}:+s}: ${(j:, :)installed}{rst}"
        }
        if (( ${#failed} )) {
            +zi-message "{annex}linkman-annex{ehi}: {error}Could not remove man page${${${#failed}/1/}:+s}: ${(j:, :)failed}{rst}"
        }
    }
}
